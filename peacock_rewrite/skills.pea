import { Highlight } from "./misc"
import { skill_context, skills } from "./store"

class Item < Element =
  context = skill_context
  margin_right { margin: true } = "5px"
  margin_right _ = ""
  style props _ = "
    margin-right: #{margin_right props};
    color: unset;
    a {
      &:active {
        color: gray;
      }
    }
  "
  show_comma { comma: true } = ","
  show_comma _ = nil
  set_active_skill selected_skill =
    if selected_skill == props[:name]
      set_context nil
    else
      set_context props[:name]
    end
  view { name } _ selected_skill =
    <span>
      <a onclick=#{set_active_skill selected_skill}>
        <Highlight active={name == selected_skill}>
          {name}
        </Highlight>
      </a>
      {show_comma props}
    </span>

class Skill < Element =
  style _ _ = "
    margin-right: 5px;
    div {
      display: inline;
    }
  "
  view { parent, sub_skills } _ _ =
    <div>
      <Item name={parent} margin />
      {"("}
      {sub_skills.map fn name i l =>
        <div>
          <Item
            name={name}
            comma={i < l.last_index}
            margin={i < l.last_index}
          />
        </div>
      end}
      {")"}
    </div>

class SkillRow < Element =
  style _ _ = "
    display: flex;
    justify-content: center;
    margin: 0;
    padding: 0;
  "
  view { skills } _ _ =
    <div>
      {skills.map #{<Skill parent={%[:parent]} sub_skills={%[:sub_skills]} />}}
    </div>

export class Skills < Element =
  style _ _ = "
    margin: 10px 20% 0 20%;
    display: flex;
    justify-content: center;
    margin-bottom: 20px;
  "
  view _ _ _ =
    <div>
      <div>
        {skills.map #{<SkillRow skills={%} />}}
      </div>
    </div>
